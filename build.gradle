import java.text.DateFormat
import java.text.SimpleDateFormat

repositories {
    mavenCentral()
    google()
}

buildscript {

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'
        classpath 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.12.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: "com.github.hierynomus.license"

license {
    header rootProject.file('src/main/etc/header.txt')
}

def buildVersionName() {
    DateFormat df = new SimpleDateFormat("yyyy-MM-dd-HH-mm-ss")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    String versionName = "1-" + df.format(new Date())
    File versionApkFile = new File(projectDir, "src/main/assets/versionApk.txt")
    versionApkFile.text = versionName
    return versionName
}

android {
    compileSdkVersion 22
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "org.esupportail.nfctagdroid"
        minSdkVersion 19
        targetSdkVersion 19
        versionCode 1
        versionName buildVersionName()
    }

    signingConfigs {
        release {
            storeFile file("esup-android-apps.keystore")
            storePassword "esupnfctag"
            keyAlias "LeoDroidApp"
            keyPassword "esupnfctag"
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.release
            versionNameSuffix "-dev"
        }
        release {
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/NOTICE' // will not include NOTICE file
        exclude 'META-INF/LICENSE' // will not include LICENSE file
    }
}


dependencies {
    api 'com.android.support:appcompat-v7:22.2.1'
    api 'org.slf4j:slf4j-api:1.7.19'
    api 'com.github.tony19:logback-android-classic:1.1.1-4'
    api 'com.github.tony19:logback-android-core:1.1.1-4'
    api 'com.sun.mail:android-mail:1.5.5'
    api 'com.sun.mail:android-activation:1.5.5'
    api 'com.fasterxml.jackson.core:jackson-databind:2.4.1'
    api 'commons-io:commons-io:2.5'
}





